plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.dagger.hilt.android'
    id 'com.google.devtools.ksp'
}

def commit = providers.exec {
    commandLine("git", "rev-parse", "--short", "HEAD")
}.standardOutput.asText.get().trim()

android {
    namespace 'org.introskipper.segmenteditor'

    defaultConfig {
        applicationId "org.introskipper.segmenteditor"
        compileSdk 36
        minSdk 30
        targetSdkVersion 35
        versionName "1.0.6"
        versionCode = versionName.replace(".","").toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField "String", "COMMIT", "\""+commit+"\""
    }
    androidResources {
        generateLocaleConfig = false
    }

    flavorDimensions += "source"
    productFlavors {
        github {
            dimension "source"
            manifestPlaceholders.installPermission = "android.permission.REQUEST_INSTALL_PACKAGES"
            manifestPlaceholders.updatesPermission = "android.permission.UPDATE_PACKAGES_WITHOUT_USER_ACTION"
            manifestPlaceholders.queriesPermission = "android.permission.QUERY_ALL_PACKAGES"
            buildConfigField "boolean", "GOOGLE_PLAY", "false"
        }
        google {
            dimension "source"
            isDefault true
            manifestPlaceholders.installPermission = "${defaultConfig.applicationId}.INSTALL"
            manifestPlaceholders.updatesPermission = "${defaultConfig.applicationId}.UPDATES"
            manifestPlaceholders.queriesPermission = "${defaultConfig.applicationId}.QUERIES"
            buildConfigField "boolean", "GOOGLE_PLAY", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    try {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
        signingConfigs {
            document {
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.document
        buildTypes.debug.signingConfig signingConfigs.document
    } catch (FileNotFoundException ignored) { }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        compose true
        buildConfig = true
    }
    composeOptions {
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion '1.5.5'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += '/META-INF/versions/9/OSGI-INF/MANIFEST.MF'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "${rootProject.name}-${commit}-${output.baseName}.apk"
        }
    }
}

dependencies {
    // You will need this to load local JS and CSS files in the WebView
    implementation "androidx.webkit:webkit:1.15.0"

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.core:core-pip:1.0.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.4'
    implementation platform('androidx.compose:compose-bom:2026.02.00')
    implementation 'androidx.compose.animation:animation-core'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.documentfile:documentfile:1.1.0'

    // Navigation Compose
    implementation 'androidx.navigation:navigation-compose:2.9.7'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.10.2'

    // Networking for Jellyfin API
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.3.2'
    implementation 'androidx.browser:browser:1.9.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'

    // ML Kit for dynamic translation
    implementation 'com.google.mlkit:translate:17.0.3'

    // ExoPlayer for video playback
    ext.media3_version = '1.9.1'
    implementation "androidx.media3:media3-datasource-okhttp:$media3_version"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-exoplayer-hls:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"
    implementation "androidx.media3:media3-ui-compose:$media3_version"
    ext.nextlib_version = "$media3_version-0.11.0"
    implementation("io.github.anilbeesetti:nextlib-media3ext:$nextlib_version")
    implementation("io.github.anilbeesetti:nextlib-mediainfo:$nextlib_version")

    // Security for encrypted preferences
    implementation 'androidx.security:security-crypto:1.1.0'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.10.0'

    // Image loading
    implementation 'io.coil-kt:coil-compose:2.7.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'

    // Swipe refresh
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.36.0'

    // Hilt for dependency injection
    implementation "com.google.dagger:hilt-android:$dagger_version"
    ksp "com.google.dagger:hilt-compiler:$dagger_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

}
