import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id "com.github.node-gradle.node"
}

android {
    namespace 'org.introskipper.segmenteditor'

    defaultConfig {
        applicationId "org.introskipper.segmenteditor"
        compileSdk 36
        minSdk 30
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        def commit = providers.exec {
            commandLine("git", "rev-parse", "--short", "HEAD")
        }.standardOutput.asText.get().trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "${rootProject.name}-${commit}"
    }
    androidResources {
        generateLocaleConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    try {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
        signingConfigs {
            document {
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.document
        buildTypes.debug.signingConfig signingConfigs.document
    } catch (FileNotFoundException ignored) { }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        compose true
        buildConfig = true
    }
    composeOptions {
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion '1.5.5'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            output.outputFileName = project.archivesBaseName + "-${output.baseName}.apk"
        }
    }
}

dependencies {
    // You will need this to load local JS and CSS files in the WebView
    implementation "androidx.webkit:webkit:1.15.0"

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.2'
    implementation platform('androidx.compose:compose-bom:2026.01.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3:1.4.0'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.documentfile:documentfile:1.1.0'
    
    // Navigation Compose
    implementation 'androidx.navigation:navigation-compose:2.8.5'
    
    // Networking for Jellyfin API
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    
    // ExoPlayer for video playback
    implementation 'androidx.media3:media3-exoplayer:1.5.0'
    implementation 'androidx.media3:media3-ui:1.5.0'
    implementation 'androidx.media3:media3-common:1.5.0'
    
    // Security for encrypted preferences
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    
    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.10.0'
    
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation platform('androidx.compose:compose-bom:2026.01.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.10.1'
}

/**
 * Node plugin configuration
 * https://github.com/node-gradle/gradle-node-plugin/blob/master/docs/usage.md#configuring-the-plugin
 */
node {
    // The Node.js project directory location
    // This is where the package.json file and node_modules directory are located
    // By default it is at the root of the current project
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        npmCommand =  '/usr/local/bin/npm'
    }
    nodeProjectDir.set(file("../../web"))
}

tasks.register('runGitClean', Exec) {
    commandLine 'rm', '-r', 'web'
    workingDir file('../..')
    ignoreExitValue = true
}
tasks.register('runGitClone', Exec) {
    commandLine 'git', 'clone', 'https://github.com/intro-skipper/segment-editor', 'web'
    workingDir file('../..')
    standardOutput = System.out
    errorOutput = System.err
}
tasks.register('runGitCloud', Exec) {
    commandLine 'git', 'apply', '../android.patch'
    workingDir file('../../web')
    standardOutput = System.out
    errorOutput = System.err
}
runGitClone.dependsOn runGitClean
runGitCloud.dependsOn runGitClone
npmInstall.dependsOn runGitCloud

/**
 * This task builds the vite project at ../../web folder.
 */
def buildWeb = tasks.register('buildWeb', NpmTask) {
    args = ['run', 'build']
    doFirst {
        println 'Executing `npm run build` in ../../web'
    }
    dependsOn npmInstall
}

/**
 * Removes the assets generated by the `buildWeb` task.
 */
def cleanWeb = tasks.register('cleanWeb', Delete) {
    description = 'Removes web assets generated by vite'
    delete '../../web/dist'
}

/**
 * This task clears the Android assets folder.
 * This is necessary because the Android build process does not clear the assets folder.
 */
def cleanAssets = tasks.register('cleanAssets', Delete) {
    delete 'src/main/assets/dist'
    dependsOn buildWeb
}

/**
 * This task copies the the vite project at ../../web/dist folder to the Android assets folder.
 */
def copyDistToAssets = tasks.register('copyDistToAssets', Copy) {
    from '../../web/dist'
    into 'src/main/assets/dist'
    dependsOn buildWeb
    dependsOn cleanAssets
}

/*
 * Hooks the custom `copyDistToAssets` task into running before the app build.
 */
tasks.named("preBuild") {
    dependsOn copyDistToAssets
}

/** Include cleanAssets when running a project clean */
tasks.named('clean') {
    dependsOn cleanWeb
    dependsOn cleanAssets
}
